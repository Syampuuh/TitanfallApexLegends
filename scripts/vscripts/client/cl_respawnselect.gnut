global function ClRespawnselect_MP_Init


//
//
//

//
//
//

const RESPAWN_SELECTIONS = 2
const PRE_SHOW_WAIT = 1.5
const RESPAWN_BUILTINS = 2
const TITLE2_ENABLED = 0

const SELECT_NONE 			= 0
const SELECT_DEAD			= 1
const SELECT_TITAN_PILOT 	= 2
const SELECT_PREMATCH		= 4

const SELECT_HUD_TITAN = 0
const SELECT_HUD_PILOT = 1

enum eTitanBindStyle
{
	ENTER,
	RSHIFT,
	LSHIFT
}

struct
{
	var respawnHintRui

	bool commandsRegistered = false
	int spawnAsTitanBindStyleUsed = eTitanBindStyle.ENTER
} file


void function ClRespawnselect_MP_Init()
{
    //
    //
    //
    //
    //
    //

	RegisterSignal( "TitanReadyPulse" )
	RegisterSignal( "RefreshDelayed" )

	file.respawnHintRui = CreatePermanentCockpitRui( $"ui/respawn_hint.rpak", HUD_Z_BASE )

	RegisterServerVarChangeCallback( "gameState", RespawnSelect_GameStateChanged )
	RegisterServerVarChangeCallback( "titanAvailableBits0", RespawnSelect_OnTitanAvailableChanged )
	RegisterServerVarChangeCallback( "titanAvailableBits1", RespawnSelect_OnTitanAvailableChanged )
	RegisterServerVarChangeCallback( "titanAvailableBits2", RespawnSelect_OnTitanAvailableChanged )
	RegisterServerVarChangeCallback( "titanAvailableBits3", RespawnSelect_OnTitanAvailableChanged )
	RegisterServerVarChangeCallback( "respawnAvailableBits0", RespawnSelect_OnRespawnAvailableChanged )
	RegisterServerVarChangeCallback( "respawnAvailableBits1", RespawnSelect_OnRespawnAvailableChanged )
	RegisterServerVarChangeCallback( "respawnAvailableBits2", RespawnSelect_OnRespawnAvailableChanged )
	RegisterServerVarChangeCallback( "respawnAvailableBits3", RespawnSelect_OnRespawnAvailableChanged )
	RegisterServerVarChangeCallback( "gameEndTime", RespawnSelection_GameEndTimeChanged )

	AddCallback_OnPlayerLifeStateChanged( RespawnSelect_LifeStateChanged )
}

void function RespawnSelect_GameStateChanged()
{
	UpdateRespawnSelectHud()
}

void function RespawnSelect_OnTitanAvailableChanged()
{
	UpdateRespawnSelectHud()
}

void function RespawnSelect_OnRespawnAvailableChanged()
{
	UpdateRespawnSelectHud()
}

void function RespawnSelection_GameEndTimeChanged()
{
	UpdateRespawnSelectHud()
}

void function RespawnSelect_LifeStateChanged( entity player, int oldLifeState, int newLifeState )
{
	UpdateRespawnSelectHud()
}

void function UpdateRespawnSelectHud()
{
	entity player = GetLocalClientPlayer()

	if ( RespawnSelect_ShouldShowRespawnAsPilot( player ) )
	{
		RegisterRespawnCommands()
		RuiSetBool( file.respawnHintRui, "isVisible", true )
		RuiSetBool( file.respawnHintRui, "isTitanAvailable", RespawnSelect_ShouldShowResapwnAsTitan( player ) )
		RuiSetBool( file.respawnHintRui, "isTitanAlive", RespawnSelect_CanPlayerSpawnIntoTitan( player ) )
		RuiSetGameTime( file.respawnHintRui, "nextSpawnTime", GetNextRespawnTime( player ) )
		RuiSetString( file.respawnHintRui, "titanSpawnAsText", GetTitanSpawnAsText() )
		RuiSetBool( file.respawnHintRui, "shouldShowSkipReplay", false )
	}
	else if ( RespawnSelect_ShouldShowSkipReplay( player ) )
	{
		RegisterRespawnCommands()
		RuiSetBool( file.respawnHintRui, "isVisible", true )
		RuiSetBool( file.respawnHintRui, "shouldShowSkipReplay", true )
		RuiSetBool( file.respawnHintRui, "isTitanAvailable", false )
	}
	else
	{
		DeregisterRespawnCommands()
		RuiSetBool( file.respawnHintRui, "isVisible", false )
	}
}

void function RegisterRespawnCommands()
{
	if ( file.commandsRegistered )
		return

	//
	file.spawnAsTitanBindStyleUsed = GetSpawnAsTitanBindStyle()

	//
	RegisterButtonPressedCallback( KEY_SPACE,				PlayerPressed_RespawnPilot )
	RegisterButtonPressedCallback( BUTTON_X,				PlayerPressed_RespawnPilot )

	//
	RegisterButtonPressedCallback( BUTTON_Y,				PlayerPressed_RespawnTitan )

	switch ( file.spawnAsTitanBindStyleUsed )
	{
		case eTitanBindStyle.RSHIFT:
			RegisterButtonPressedCallback( KEY_RSHIFT,				PlayerPressed_RespawnTitan)
			break
		case eTitanBindStyle.LSHIFT:
			RegisterButtonPressedCallback( KEY_LSHIFT,				PlayerPressed_RespawnTitan)
			break
		case eTitanBindStyle.ENTER:
		default:
			RegisterButtonPressedCallback( KEY_ENTER,				PlayerPressed_RespawnTitan)
			break
	}

	file.commandsRegistered = true
}

void function DeregisterRespawnCommands()
{
	if ( !file.commandsRegistered )
		return

	//
	DeregisterButtonPressedCallback( KEY_SPACE,				PlayerPressed_RespawnPilot )
	DeregisterButtonPressedCallback( BUTTON_X,				PlayerPressed_RespawnPilot )

	//
	DeregisterButtonPressedCallback( BUTTON_Y,				PlayerPressed_RespawnTitan )

	switch ( file.spawnAsTitanBindStyleUsed )
	{
		case eTitanBindStyle.RSHIFT:
			DeregisterButtonPressedCallback( KEY_RSHIFT,				PlayerPressed_RespawnTitan)
			break
		case eTitanBindStyle.LSHIFT:
			DeregisterButtonPressedCallback( KEY_LSHIFT,				PlayerPressed_RespawnTitan)
			break
		case eTitanBindStyle.ENTER:
		default:
			DeregisterButtonPressedCallback( KEY_ENTER,				PlayerPressed_RespawnTitan)
			break
	}
	file.commandsRegistered = false
}

int function GetSpawnAsTitanBindStyle()
{
	string bind = GetKeyTappedBinding( KEY_ENTER ).tolower()
	if ( bind == "say" || bind == "say_team" )
	{
		bind = GetKeyTappedBinding( KEY_RSHIFT ).tolower()
		if ( bind == "say" || bind == "say_team" )
		{
			return eTitanBindStyle.LSHIFT
		}
		else
		{
			return eTitanBindStyle.RSHIFT
		}
	}
	return eTitanBindStyle.ENTER
}

string function GetTitanSpawnAsText()
{
	switch ( file.spawnAsTitanBindStyleUsed )
	{
		case eTitanBindStyle.RSHIFT:
			return "#RESPAWNSELECT_TITAN_RSHIFT"

		case eTitanBindStyle.LSHIFT:
			return "#RESPAWNSELECT_TITAN_LSHIFT"

		case eTitanBindStyle.ENTER:
		default:
			return "#RESPAWNSELECT_TITAN"
	}

	return "#RESPAWNSELECT_TITAN"
}

void function PlayerPressed_RespawnPilot( entity player )
{
	player.ClientCommand( "CC_RespawnPlayer Pilot" )
}

void function PlayerPressed_RespawnTitan( entity player )
{
	if ( HudChat_HasAnyMessageModeStoppedRecently() )
		return

	player.ClientCommand( "CC_RespawnPlayer Titan" )
}

bool function RespawnSelect_ShouldShowRespawnAsPilot( entity player )
{
	if ( IsAlive( player ) && !IsWatchingKillReplay() )
	{
		//
		return false
	}

	if ( IsPlayerEliminated( player ) )
	{
		//
		return false
	}

	if ( !IsRespawnAvailable( player ) )
	{
		//
		return false
	}

	if ( GetGameState() != eGameState.Playing && GetGameState() != eGameState.Epilogue && GetGameState() != eGameState.WinnerDetermined )
	{
		//
		return false
	}

	return true
}

bool function RespawnSelect_ShouldShowSkipReplay( entity player )
{
	if ( !IsWatchingKillReplay() )
	{
		//
		return false
	}

	if ( !IsPlayerEliminated( player ) )
	{
		//
		return false
	}

	int gameState = GetGameState()

	if ( gameState != eGameState.Playing && gameState != eGameState.Epilogue )
	{
		//
		return false
	}

	return true
}

//
bool function RespawnSelect_CanPlayerSpawnIntoTitan( entity player )
{
	return PlayerCanSpawnIntoTitan( player )
}

bool function RespawnSelect_ShouldShowResapwnAsTitan( entity player )
{
	entity titan = player.GetPetTitan()
	if ( RespawnSelect_CanPlayerSpawnIntoTitan( player ) )
		return true
	else
		return IsTitanAvailable( player )

	unreachable
}